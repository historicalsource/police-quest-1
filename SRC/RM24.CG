[ POLICE QUEST
[ rm24.cg
[ By:  Greg Rowland, Ed Anderson, Jim Walls and Al Lowe
[ For: Sierra On-Line Inc. 11/11/86
[ Last Update October 12, 1987GR

[ rm.map24

%include "gamedefs"
%include "rm117.h"



if (init.log) {
	position( a.intersection, 75, 40);

	draw( ego);
	draw( a.intersection);

	add.to.pic( vBuildings1, 1, 4, 144, 115, 0, 4);
	add.to.pic( vBuildings1, 1, 4, 144, 077, 0, 4);
	add.to.pic( vBuildings1, 2, 0, 090, 024, 0, 4);
	add.to.pic( vBuildings1, 2, 0, 110, 024, 0, 4);
	add.to.pic( vBuildings1, 2, 3, 055, 024, 0, 4);
	add.to.pic( vBuildings1, 2, 3, 029, 024, 0, 4);
	add.to.pic( vBuildings1, 1, 3, 001, 115, 0, 4);
	add.to.pic( vBuildings1, 1, 5, 001, 080, 0, 4);

	show.pic( );
	return( );
	}


[ Logic
if ( ego.y < 33) {
	streetScene = 80;
	}
else {
	if ( ego.y > 124) {
		streetScene = 93;
		}
	else {
		streetScene = 92;
		}
	}

[ Light logics 

if (posn( a.car2, 75, 45, 85, 46)
&& intersection != east.stop) {
	observe.blocks( a.car2);
	}
else	{
	ignore.blocks( a.car2);
	}

if (posn( a.car3, 87, 27, 90, 38) 
&& intersection != north.stop) {
	observe.blocks( a.car3);
	}
else	{
	ignore.blocks( a.car3);
	}

[ Logic 
[ start.cars
[car1
if ( !stopCar1) {
	if (!posn( ego, 0, 25, 17, 39)
	&& !posn( a.suspect, 0, 25, 17, 39)) {
		if (moving1 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car1, work);
			position( a.car1, left.edge, t8y);
			ignore.blocks( a.car1);
			set.priority( a.car1, 5);
			set( moved1);
			}
		if (moving1 == 1) {
			move.obj( a.car1, t1x, t8y, 1, moved1);
			draw( a.car1);		  
			}
		}
	if (moving1 == 2) {
		move.obj( a.car1, t1x, t9y, 1, moved1);
		}
	if (moving1 == 3) {
		move.obj( a.car1, left.edge, t9y, 1, moved1);
		}
	if (moving1 == 4) {
		erase( a.car1);
		moving1 = 0;
		}
	}

[ car2
if ( !stopCar2) {
	if (!posn( ego, 0, 117, 17, 131)
	&& !posn( a.suspect, 0, 117, 17, 131)) {
		if (moving2 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car2, work);
			position( a.car2, left.edge, t10y);
			ignore.blocks( a.car2);
			set.priority( a.car2, 5);
			set( moved2);
			}
		if (moving2 == 1) {
			move.obj( a.car2, t3x, t10y, 1, moved2);
			draw( a.car2);		  
			}
		}
	if (moving2 == 2) {
		move.obj( a.car2, t4x, t8y, 1, moved2);
		}
	if (moving2 == 3) {
		move.obj( a.car2, right.edge, t8y, 1, moved2);
		}
	if (moving2 == 4) {
		erase( a.car2);
		moving2 = 0;
		}
	}


[ car3
if ( !stopCar3) {
	if (!posn( ego, 133, 0, 143, 25)
	&& !posn( a.suspect, 133, 0, 143, 25)) {
		if (moving3 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car3, work);
			position( a.car3, t5x, 5);
			ignore.blocks( a.car3);
			set.priority( a.car3, 5);
			move.obj( a.car3, t5x, t7y, 1, moved3);
			draw( a.car3);		  
			++ moving3;
			}
		}
	if (moving3 == 2) {
		move.obj( a.car3, t2x, t7y, 1, moved3);
		}
	if (moving3 == 3) {
		move.obj( a.car3, t2x, top.edge, 1, moved3);
		}
	if (moving3 == 4) {
		erase( a.car3);
		moving3 = 0;
		}
	}

[*****
:exit
[*****

%message 1
"You are near the Lytton Courthouse and the City Jail."

if ((said( look) ||
	said( look, room) ||
	said( look, town)))	{
	print( 1);
	}


