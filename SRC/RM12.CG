[ POLICE QUEST
[ rm12.cg
[ By:  Greg Rowland, Ed Anderson, Jim Walls and Al Lowe
[ For: Sierra On-Line Inc. 11/11/86
[ Last Update October 4, 1987GR

[ rm.map12

%include "gamedefs"
%include "rm117.h"



if (init.log) {

	add.to.pic( vBuildings1, 0, 4, 028, 167, 0, 4);
	add.to.pic( vBuildings1, 1, 3, 001, 167, 0, 4);
	add.to.pic( vBuildings1, 0, 5, 044, 167, 0, 4);
	add.to.pic( vBuildings1, 1, 2, 059, 167, 0, 4);
	add.to.pic( vBuildings1, 1, 4, 086, 167, 0, 4);
	add.to.pic( vBuildings1, 1, 2, 106, 167, 0, 4);
	add.to.pic( vBuildings1, 0, 3, 116, 167, 0, 4);
	add.to.pic( vBuildings1, 0, 5, 144, 167, 0, 4);
	add.to.pic( vBuildings1, 1, 2, 001, 078, 0, 4);
	add.to.pic( vBuildings1, 1, 2, 001, 114, 0, 4);
	add.to.pic( vBuildings1, 2, 2, 038, 068, 0, 4);
	add.to.pic( vBuildings1, 2, 2, 049, 068, 0, 4);
	add.to.pic( vBuildings1, 2, 1, 114, 068, 0, 4);
	add.to.pic( vBuildings1, 2, 0, 088, 116, 0, 4);
	add.to.pic( vBuildings1, 2, 0, 111, 116, 0, 4);

	position( a.intersection, 75, 40);

	draw( ego);
	show.pic( );
	return( );
	}

if ( ego.y < 33) {
	streetScene = 93;
	}
else {
	if ((posn( ego, 17, 39, 22, 99) ||
		posn( ego, 47, 117, 75, 124) ||
		posn( ego, 27, 124, 55, 131) ||
		posn( ego, 85, 124, 113, 131))) {
		streetScene = 92;
		}
	else {
		streetScene = 80;
		}
	}






[ Logic 
[ Traffic logic 
[ start.cars
[car1

if ( !stopCar1) {
	if (!posn( ego, 143, 25, 159, 39)
	&& !posn( a.suspect, 143, 25, 159, 39)) {
		if (moving1 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car1, work);
			position( a.car1, right.edge, t7y);
			ignore.blocks( a.car1);
			set.priority( a.car1, 5);
			set( moved1);
			}
		if (moving1 == 1) {
			move.obj( a.car1, t4x, t7y, 1, moved1);
			draw( a.car1);		  
			}
		}
	if (moving1 == 2) {
		move.obj( a.car1, t3x, t9y, 1, moved1);
		}
	if (moving1 == 3) {
		move.obj( a.car1, t2x, t9y, 1, moved1);
		}
	if (moving1 == 4) {
		move.obj( a.car1, t1x, bottom.edge, 1, moved1);
		}
	if (moving1 == 5) {
		erase( a.car1);
		moving1 = 0;
		}
	}

[ car2
if ( !stopCar2) {

	if (!posn( ego, 0, 25, 17, 39)
	&& !posn( a.suspect, 0, 25, 17, 39)) {
		if (moving2 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car2, work);
			position( a.car2, left.edge, t8y);
			ignore.blocks( a.car2);
			set.priority( a.car2, 5);
			set( moved2);
			}
		if (moving2 == 1) {
			move.obj( a.car2, t1x, t8y, 1, moved2);
			draw( a.car2);		  
			}
		}
	if (moving2 == 2) {
		move.obj( a.car2, t1x, t9y, 1, moved2);
		}
	if (moving2 == 3) {
		move.obj( a.car2, t5x, t10y, 1, moved2);
		}
	if (moving2 == 4) {
		move.obj( a.car2, t5x, bottom.edge, 1, moved2);
		}
	if (moving2 == 5) {
		erase( a.car2);
		moving2 = 0;
		}
	}

[ car3
if ( !stopCar3) {
	if (!posn( ego, 133, 131, 143, 167)
	&& !posn( a.suspect, 133, 131, 143, 167)) {
		if (moving3 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car3, work);
			position( a.car3, t6x, bottom.edge);
			ignore.blocks( a.car3);
			set.priority( a.car3, 5);
			set( moved3);
			}
		if (moving3 == 1) {
			move.obj( a.car3, t6x, t10y, 1, moved3);
			draw( a.car3);		  
			}
		}
	if (moving3 == 2) {
		move.obj( a.car3, t4x, t9y, 1, moved3);
		}
	if (moving3 == 3) {
		move.obj( a.car3, t4x, t8y, 1, moved3);
		}
	if (moving3 == 4) {
		move.obj( a.car3, left.edge, t7y, 1, moved3);
		}
	if (moving3 == 5) {
		erase( a.car3);
		moving3 = 0;
		}
	}


if (moving0 == 255) {	[ other vehicle drawn from rm119
[ hit bikes car 
	if (biker.scene && biker.scene < 4) {
		current.view( ego, view);
		if ((view == 99 || view == 101)) {
			current.loop( ego, loop);
			[ position( a.suspect, 43, 115); rem length
			if (loop > 1) {
				if (posn( ego, 40, 114, 51, 117)) {		[ up down
					crashing = 50;
					}
				}
			else {
				[ -03, -05, 0, +01
				if (posn( ego, 43, 114, 50, 119)) {	[ left right
					crashing = 50;						[ screw diag
					}
				}
			}
		}

[ steve 
	if (current.suspect == 1) {	[ Steve
		current.view( ego, view);
		if ((view == 99 || view == 101)) {
			current.loop( ego, loop);
			[ position( a.suspect, 43, 115);	rem loop left right
			if (loop > 1) {
				if (posn( ego, 43, 115, 46, 120)) {		[ up down
					crashing = 50;
					}
				}
			else {
				[ -03, -05, 0, +01
				if (posn( ego, 40, 114, 46, 116)) {	[ left right
					crashing = 50;						[ screw diag
					}
				}
			}
		}
	}

[*****
:exit
[*****

%message 1
"You patrol the streets near \"Carol's Caffeine Castle,\"
 and the famous \"Wino Willey's\" beer joint."


if ((said( look) ||
	said( look, room) ||
	said( look, town)))	{
	print( 1);
	}


