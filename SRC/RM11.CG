[ POLICE QUEST
[ rm11.cg
[ By:  Greg Rowland, Ed Anderson, Jim Walls and Al Lowe
[ For: Sierra On-Line Inc. 11/11/86
[ Last Update October 4, 1987GR

[ rm.map11

%include "gamedefs"
%include "rm117.h"



[ Room setup 

if (init.log) {
	add.to.pic( vBuildings1, 0, 4, 000, 077, 0, 4);
	add.to.pic( vBuildings1, 0, 5, 144, 077, 0, 4);
	add.to.pic( vBuildings1, 0, 2, 144, 079, 0, 4);
	add.to.pic( vBuildings1, 0, 0, 087, 112, 0, 4);
	add.to.pic( vBuildings1, 0, 1, 110, 112, 0, 4);
	add.to.pic( vBuildings1, 1, 3, 000, 113, 0, 4);
	add.to.pic( vBuildings1, 0, 2, 144, 114, 0, 4);
	add.to.pic( vBuildings1, 2, 1, 111, 162, 0, 4);
	add.to.pic( vBuildings1, 2, 1, 089, 162, 0, 4);
	add.to.pic( vBuildings1, 2, 1, 054, 162, 0, 4);
	add.to.pic( vBuildings1, 2, 1, 030, 162, 0, 4);
	add.to.pic( vBuildings1, 0, 3, 144, 167, 0, 4);

	position( a.intersection, 75, 132);
	draw( ego);
	draw( a.intersection);
	show.pic( );
	return( );
	}


if ( ego.y < 33) {
	streetScene = 93;
	}
else {
	if ( ego.y > 124) {
		streetScene = 80;
		}
	else {
		streetScene = 92;
		}
	}

[ Logic 
[ Light logic 
if (posn( a.car2, 87, 118, 90, 130) 
&& (intersection != north.stop)) {
	observe.blocks( a.car2);
	}
else {
	ignore.blocks( a.car2);		 
	}
if (posn( a.car3, 76, 136, 84, 142) 
&& (intersection != east.stop)) {
	observe.blocks( a.car3);
	}
else {
	ignore.blocks( a.car3);		 
	}

if (posn( a.car4, 70, 118, 71, 130) 
&& (intersection != north.stop)) {
	observe.blocks( a.car4);
	}
else {
	ignore.blocks( a.car4);		 
	}

[ Traffic logic 

[ start.cars
[car1
if ( !stopCar1) {
	if (!posn( ego, 0, 25, 17, 39)
	&& !posn( a.suspect, 0, 25, 17, 39)) {
		if (moving1 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car1, work);
			position( a.car1, left.edge, t8y);
			ignore.blocks( a.car1);
			set.priority( a.car1, 5);
			set( moved1);
			}
		if (moving1 == 1) {
			move.obj( a.car1, t1x, t8y, 1, moved1);
			draw( a.car1);		 
			}
		}
	if (moving1 == 2) {
		move.obj( a.car1, t1x, bottom.edge, 1, moved1);
		}
	if (moving1 == 3) {
		erase( a.car1);
		moving1 = 0;
		}
	}
		
[ car2
if ( !stopCar2) {
	if (!posn( ego, 143, 25, 159, 39)
	&& !posn( a.suspect, 143, 25, 159, 39)) {
		if (moving2 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car2, work);
			position( a.car2, right.edge, t7y);
			ignore.blocks( a.car2);
			set.priority( a.car2, 5);
			set( moved2);
			}
		if (moving2 == 1) {
			move.obj( a.car2, t6x, t7y, 1, moved2);
			draw( a.car2);		 
			}
		}
	if (moving2 == 2) {
		move.obj( a.car2, t5x, t9y, 1, moved2);
		}
	if (moving2 == 3) {
		move.obj( a.car2, t4x, t9y, 1, moved2);
		}
	if (moving2 == 4) {
		move.obj( a.car2, t3x, bottom.edge, 1, moved2);
		}
	if (moving2 == 5) {
		erase( a.car2);
		moving2 = 0;
		}
	}

[ car3
if ( !stopCar3) {
	if (!posn( ego, 75, 131, 85, 167)
	&& !posn( a.suspect, 75, 131, 85, 167)) {
		if (moving3 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car3, work);
			position( a.car3, t4x, bottom.edge);
			ignore.blocks( a.car3);
			set.priority( a.car3, 5);
			set( moved3);
			}
		if (moving3 == 1) {
			move.obj( a.car3, t4x, t8y, 1, moved3);
			draw( a.car3);		 
			}
		}
	if (moving3 == 2) {
		move.obj( a.car3, left.edge, t7y, 1, moved3);
		}
	if (moving3 == 3) {
		erase( a.car3);			 
		moving3 = 0;
		}
	}

[ car4
if ( !stopCar4) {
	if (!posn( ego, 17, 131, 27, 167)
	&& !posn( a.suspect, 17, 131, 27, 167)) {
		if (moving4 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car4, work);
			position( a.car4, t2x, bottom.edge);
			ignore.blocks( a.car4);
			set.priority( a.car4, 5);
			set( moved4);
			}
		if (moving4 == 1) {
			move.obj( a.car4, t2x, t10y, 1, moved4);
			draw( a.car4);		 
			}
		}
	if (moving4 == 2) {
		move.obj( a.car4, t5x, t10y, 1, moved4);	
		}
	if (moving4 == 3) {
		move.obj( a.car4, t6x, t8y, 1, moved4);
		}
	if (moving4 == 4) {
		move.obj( a.car4, right.edge, t8y, 1, moved4);
		}
	if (moving4 == 5) {
		erase( a.car4);			 
		moving4 = 0;
		}
	}

[*****
:exit
[*****
%message	1
"You're outside the beautiful \"Hotel Delphoria.\""

if ((said( look) ||
	said( look, room) ||
	said( look, town)))	{
	print( 1);
	}



