[ POLICE QUEST
[ rm51.cg
[ By:  Greg Rowland, Jim Walls and Al Lowe
[ For: Sierra On-Line Inc. 6/9/86
[ Last Update September 25, 1987al

[ teletype.room


%include "gamedefs"
%include "rm51.msg"



%define	at.ego			lf0
%define	noMoreLaura		lf1
%define	nearComputer		lf4
%define	nearWindow		lf5

%define	computer.on		tf2		[	NOTE: shared with room 112


%define	laura			lcl0
%define	lauraX			lcl1
%define	lauraY			lcl2

						
%object	aLaura			1
						
						

if (computer.on) {
	call.f( dynamic1);				[lgc.computer
	return( );
	}


if (init.log) {
	set( init.dyn);
	dynamic1 = 112;				[lgc.computer

	load.pic( current.room);
	load.pic( dynamic1);

	load.logics.f( dynamic1);

	if (current.sonny != sonny.narc) {
		load.view( v.laura);
		set.view( aLaura, v.laura);
		reset( LauraInRoom);
		lauraX = 74;
		lauraY = 167;
		}
	
	if (priorX) {
		set.loop.f( ego, loop);
		position.f( ego, priorX, priorY);
		priorX = 0;
		}
	else {
		position( ego, 74, 167);
		}
	}



if (init.dyn) {
	reset( init.dyn);

	draw.pic( current.room);
	
	animate.obj( ego);
	animate.obj( aLaura);

	if (!noMoreLaura &&
		current.sonny != sonny.narc)
		{
		position.f( aLaura, lauraX, lauraY);

		if (LauraInRoom)
			{
			draw( aLaura);
			}
		else
			{
			scriptTimer = 30;
			script = 0;
			}
		}

	draw( ego);

	show.pic( );
	status.line.on( );
	set( enable.menu);

	return( );
	}


[ Logic 

reset( nearWindow);
if (posn( ego, 89, 115, 113, 123)) {
	{
	set( nearWindow);
	}

reset( nearComputer);
if (posn( ego, 42, 115, 87, 126))
	{
	set( nearComputer);
	}

if (edge.ego.hit == bottom)
	{
	reset( LauraInRoom);
	new.room( 6);					[rm.hqhall1
	}

distance( ego, aLaura, laura);
if (laura < dist) {
	set( nearPerson);
	}


if (LauraInRoom)					[	stop Laura's pedaling 
	{
	get.posn( aLaura, x, y);
	get.posn( aLaura, lauraX, lauraY);
	if ((work1 != x || work2 != y))
		{
		work1 = x;
		work2 = y;
		start.cycling( aLaura);
		}
	else
		{
		stop.cycling( aLaura);
		}

	work5 = x;					[ Set Laura loop
	x -= 7;
	work5 += 13;
	if (ego.x < x)
		{
		set.loop( aLaura, 1);
		}
	else
		{
		if (ego.x > work5)
			{
			set.loop( aLaura, 0);
			}
		else
			{
			if (ego.y < y)
				{
				set.loop( aLaura, 3);
				}
			else
				{
				set.loop( aLaura, 2);
				}
			}
		}
	}


if (scriptDone)
	{
	reset( scriptDone);
	++script;


	[	Laura pops in to say, "Get out of here, asshole!"

	if (script == 1)
		{
		set( LauraInRoom);
		position( aLaura, 74, 167);
		set.loop( aLaura, facing.back);
		draw( aLaura);
		print( 15);				[ Det laura watts enters
		move.obj( aLaura, 44, 137, 1, scriptDone);
		}

	if (script == 2)
		{
		set.loop( aLaura, facing.right);
		scriptCycles = 11;
		}

	if (script == 3)
		{
		scriptTimer = 20;
		if (!hasBrief1)
			{
			print( 3);
			}
		else
			{
			print( 2);			[ should be out on duty
			}
		}

	if (script == 4)
		{
		print( 13);
		move.obj( aLaura, 74, 167, 1, scriptDone);
		}

	if (script == 5)
		{
		erase( aLaura);
		reset( LauraInRoom);
		set( noMoreLaura);
		}
	}
		


[ Text 

if (!have.input) {
	goto exit;
	}

if ((said( boot) ||
	said( boot, computer) ||
	said( boot, up, computer) ||
	said( boot, up) ||
	said( power) ||
	said( use, computer) ||
	said( turn, on, power) ||
	said( turn, power, on) ||
	said( turn, on, computer) ||
	said( turn, computer, on)))
	{
	if (posn( ego, 68, 118, 85, 124)) {
		set( init.dyn)
		set( computer.on)
		unanimate.all( );

		status.line.off( );
		call.f( dynamic1);
		}
	else {
		set( notCloseEnough);
		}
	}

if ((said( quit) ||
	said( power) ||
	said( turn, off, power) ||
	said( turn, power, off) ||
	said( shut, off, power) ||
	said( shut, power, off) ||
	said( turn, off, computer) ||
	said( turn, computer, off) ||
	said( shut, off, computer) ||
	said( shut, computer, off)))
	{
	if (nearComputer) {
		print( 18);				[ does it look like its on
		}
	else {
		set( notCloseEnough);
		}
	}

if (said( look, computer)) {
	if (nearComputer) {
		print( 4);
		}
	else {
		set( notCloseEnough);
		}
	}

if (said( look, table)) {
	if (nearComputer) {
		print( 17);
		}
	else {
		set( notCloseEnough);
		}
	}

if ((said( look) ||
	said( look, room))) {
	print( 1);
	}

if ((said( look, board, rol) ||
	said( look, bulletin, board, rol))) {
	if (posn( ego, 29, 122, 40, 147)) {
		print( 9);
		}
	else {
		set( notCloseEnough);
		}
	}

if (said( look, wall, rol)) {
	print( 21);
	}

if (said( look, shelves)) {
	if (nearComputer) {
		print( 7);
		}
	else {
		set( notCloseEnough);
		}
	}

if (said( look, book)) {
	print( 16);
	}

if (said( get, book)) {
	if (nearComputer) {
		print( 8);
		}
	else {
		set( notCloseEnough);
		}
	}

if ((said( look, out, window) ||
	said( look, window))) {
	if (nearWindow) {
		print( 5);
		}
	else {
		set( notCloseEnough);
		}
	}

if (said( open, window)) {
	if (nearWindow) {
		print( 6);
		}
	else {
		set( notCloseEnough);
		}
	}

if ((said( look, ceiling) ||
	said( look, up))) {
  	print( 10);
	}

if ((said( look, floor) ||
	said( look, down))) {
  	print( 11);
	}

if (said( look, under, table)) {
	if (nearComputer) {
		print( 12);
		}
	else {
		set( notCloseEnough);
		}
	}


if (LauraInRoom)
	{
	if ((said( talk, girl) ||
		said( talk, partner))) {
		print( 19);
		}

	if ((said( kiss, girl) ||
		said( kiss, partner))) {
		print( 20);
		}
	}


		
[
:exit
[

