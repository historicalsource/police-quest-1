[ POLICE QUEST
[ rm14.cg
[ By:  Greg Rowland, Ed Anderson, Jim Walls and Al Lowe
[ For: Sierra On-Line Inc. 11/11/86
[ Last Update October 4, 1987GR

[ rm.map14

%include "gamedefs"
%include "rm117.h"



[ Room setup 

if (init.log) {
	draw( ego);

	add.to.pic( vBuildings0, 0, 0, 028, 113, 0, 4);
	add.to.pic( vBuildings0, 1, 0, 053, 113, 0, 4);
	add.to.pic( vBuildings0, 1, 1, 111, 113, 0, 4);
	add.to.pic( vBuildings0, 0, 1, 086, 113, 0, 4);
	add.to.pic( vBuildings0, 2, 1, 051, 161, 0, 4);
	add.to.pic( vBuildings0, 2, 1, 029, 161, 0, 4);
	add.to.pic( vBuildings0, 2, 1, 087, 161, 0, 4);
	add.to.pic( vBuildings0, 2, 1, 109, 161, 0, 4);
	add.to.pic( vBuildings0, 0, 3, 144, 167, 0, 4);
	add.to.pic( vBuildings0, 0, 3, 144, 077, 0, 4);
	add.to.pic( vBuildings0, 0, 3, 144, 115, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 030, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 045, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 059, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 088, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 103, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 118, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 146, 024, 0, 4);
	add.to.pic( vBuildings0, 1, 4, 124, 064, 0, 4);
	add.to.pic( vBuildings0, 1, 5, 046, 051, 0, 4);
	add.to.pic( vBuildings0, 1, 6, 054, 111, 0, 4);

	show.pic( );
	return( );
	}

[ Logic

if ( ego.x < 22) {
	streetScene = 93;
	}
else {
	streetScene = 80;
	}


[ Traffic logic 
[ start.cars
[car1
if ( !stopCar1) {
	if (!posn( ego, 143, 25, 159, 39)
	&& !posn( a.suspect, 143, 25, 159, 39)) {
		if (moving1 == 0)
			{
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car1, work);
			position( a.car1, right.edge, t7y);
			ignore.blocks( a.car1);
			set.priority( a.car1, 5);
			set( moved1);
			}
		if (moving1 == 1) {
			move.obj( a.car1, t4x, t7y, 1, moved1);
			draw( a.car1);		  
			}
		}
	if (moving1 == 2) {
		move.obj( a.car1, t3x, t9y, 1, moved1);
		}
	if (moving1 == 3) {
		move.obj( a.car1, t2x, t9y, 1, moved1);
		}
	if (moving1 == 4) {
		move.obj( a.car1, t1x, bottom.edge, 1, moved1);
		}
	if (moving1 == 5) {
		erase( a.car1);
		moving1 = 0;
		}
	}

[ car2
if ( !stopCar2) {
	if (!posn( ego, 17, 131, 27, 167)
	&& !posn( a.suspect, 17, 131, 27, 167)) {
		if (moving2 == 0)
			{
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car2, work);
			position( a.car2, t2x, bottom.edge);
			ignore.blocks( a.car2);
			set.priority( a.car2, 5);
			set( moved2);
			}
		if (moving2 == 1) {
			move.obj( a.car2, t2x, t10y, 1, moved2);
			draw( a.car2);		  
			}
		}
	if (moving2 == 2) {
		move.obj( a.car2, t5x, t10y, 1, moved2);
		}
	if (moving2 == 3) {
		move.obj( a.car2, t5x, bottom.edge, 1, moved2);
		}
	if (moving2 == 4) {
		erase( a.car2);
		moving2 = 0;
		}
	}

[ car3
if ( !stopCar3) {
	if (!posn( ego, 133, 131, 143, 167)
	&& !posn( a.suspect, 133, 131, 143, 167)) {
		if (moving3 == 0)
			{
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car3, work);
			position( a.car3, t6x, bottom.edge);
			ignore.blocks( a.car3);
			set.priority( a.car3, 5);
			set( moved3);
			}
		if (moving3 == 1) {
			move.obj( a.car3, t6x, t10y, 1, moved3);
			draw( a.car3);		  
			}
		}
	if (moving3 == 2) {
		move.obj( a.car3, t4x, t9y, 1, moved3);
		}
	if (moving3 == 3) {
		move.obj( a.car3, t4x, t8y, 1, moved3);
		}
	if (moving3 == 4) {
		move.obj( a.car3, t2x, t7y, 1, moved3);
		}
	if (moving3 == 5) {
		move.obj( a.car3, t2x, top.edge, 1, moved3);
		}
	if (moving3 == 6) {
		erase( a.car3);
		moving3 = 0;
		}
	}

		
[*****
:exit
[*****

%message 1
"To the west is the entrance to Lytton City Park."


if ((said( look) ||
	said( look, room) ||
	said( look, town)))	{
	print( 1);
	}


