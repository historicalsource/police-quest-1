[ POLICE QUEST
[ rm21.cg
[ By:  Greg Rowland, Ed Anderson, Jim Walls and Al Lowe
[ For: Sierra On-Line Inc. 11/11/86
[ Last Update October 4, 1987GR

[ rm.map21

%include "gamedefs"
%include "rm117.h"



if (init.log) {
	position( a.intersection, 75, 132);

	draw( ego);
	draw( a.intersection);

	add.to.pic( vBuildings0, 2, 1, 029, 160, 0, 4);
	add.to.pic( vBuildings0, 2, 1, 051, 160, 0, 4);
	add.to.pic( vBuildings0, 2, 1, 087, 160, 0, 4);
	add.to.pic( vBuildings0, 2, 1, 109, 160, 0, 4);
	add.to.pic( vBuildings0, 0, 0, 028, 114, 0, 4);
	add.to.pic( vBuildings0, 1, 0, 053, 114, 0, 4);
	add.to.pic( vBuildings0, 0, 0, 086, 114, 0, 4);
	add.to.pic( vBuildings0, 1, 0, 111, 114, 0, 4);
	add.to.pic( vBuildings0, 1, 2, 000, 167, 0, 4);
	add.to.pic( vBuildings0, 1, 3, 000, 078, 0, 4);
	add.to.pic( vBuildings0, 1, 3, 000, 113, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 000, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 030, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 045, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 061, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 088, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 104, 024, 0, 4);
	add.to.pic( vBuildings0, 2, 0, 119, 024, 0, 4);
	add.to.pic( vBuildings0, 0, 4, 041, 064, 0, 4);
	add.to.pic( vBuildings0, 0, 4, 099, 086, 0, 4);
	add.to.pic( vBuildings0, 0, 6, 054, 111, 0, 4);

	show.pic( );
	return( );
	}	

[ Logic


if ( ego.x > 138) {
	streetScene = 93;
	}
else {
	streetScene = 80;
	}

[ Light logic 
if (posn( a.car2, 87, 118, 90, 130) 
&& (intersection != north.stop)) {
	observe.blocks( a.car2);
	}
else {
	ignore.blocks( a.car2);		 
	}
if (posn( a.car3, 76, 136, 84, 142) 
&& (intersection != east.stop)) {
	observe.blocks( a.car3);
	}
else {
	ignore.blocks( a.car3);		 
	}

if (posn( a.car4, 70, 118, 71, 130) 
&& (intersection != north.stop)) {
	observe.blocks( a.car4);
	}
else {
	ignore.blocks( a.car4);		 
	}

[ Traffic logic 
[ start.cars
[car1
if ( !stopCar1) {
	if (!posn( ego, 0, 25, 17, 39)
	&& !posn( a.suspect, 0, 25, 17, 39)) {
		if (moving1 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car1, work);
			position( a.car1, left.edge, t8y);
			ignore.blocks( a.car1);
			set.priority( a.car1, 5);
			set( moved1);
			}
		if (moving1 == 1) {
			move.obj( a.car1, t1x, t8y, 1, moved1);
			draw( a.car1);		  
			}
		}
	if (moving1 == 2) {
		move.obj( a.car1, t1x, bottom.edge, 1, moved1);
		}
	if (moving1 == 3) {
		erase( a.car1);
		moving1 = 0;
		}
	}

[ car2
[upper left
if ( !stopCar2)
	{
	if (!posn( ego, 133, 0, 143, 25)
	&& !posn( a.suspect, 133, 0, 143, 25)) {
		if (moving2 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car2, work);
			position( a.car2, t5x, top.edge);
			ignore.blocks( a.car2);
			set.priority( a.car2, 5);
			set( moved2);
			}
		if (moving2 == 1) {
			move.obj( a.car2, t5x, t9y, 1, moved2);
			draw( a.car2);		  
			}
		}
	if (moving2 == 2) {
		move.obj( a.car2, t4x, t9y, 1, moved2);
		}
	if (moving2 == 3) {
		move.obj( a.car2, t3x, bottom.edge, 1, moved2);
		}
	if (moving2 == 4) {
		erase( a.car2);
		moving2 = 0;
		}
	}

[ car3
if ( !stopCar3)
	{
	if (!posn( ego, 75, 131, 85, 167)
	&& !posn( a.suspect, 75, 131, 85, 167)) {
		if (moving3 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car3, work);
			position( a.car3, t4x, bottom.edge);
			ignore.blocks( a.car3);
			set.priority( a.car3, 5);
			set( moved3);
			}
		if (moving3 == 1) {
			move.obj( a.car3, t4x, t8y, 1, moved3);
			draw( a.car3);		  
			}
		}
	if (moving3 == 2) {
		move.obj( a.car3, left.edge, t7y, 1, moved3);
		}
	if (moving3 == 3) {
		erase( a.car3);			 
		moving3 = 0;
		}
	}


[ car4
[upper right
if ( !stopCar4) {
	if (!posn( ego, 17, 131, 27, 167)
	&& !posn( a.suspect, 17, 131, 27, 167)) {
		if (moving4 == 0) {
			random( 0, 9, work);
			if ( hercules && work < 4) {
				++ work;
				}

			set.cel.f( a.car4, work);
			position( a.car4, t2x, bottom.edge);
			ignore.blocks( a.car4);
			set.priority( a.car4, 5);
			set( moved4);
			}
		if (moving4 == 1) {
			move.obj( a.car4, t2x, t10y, 1, moved4);
			draw( a.car4);		  
			}
		}
	if (moving4 == 2) {
		move.obj( a.car4, t5x, t10y, 1, moved4);	
		}
	if (moving4 == 3) {
		move.obj( a.car4, t6x, top.edge, 1, moved4);
		}
	if (moving4 == 4) {
		erase( a.car4);			 
		moving4 = 0;
		}
	}


[*****
:exit
[*****
%message 1
"The Clearwater River borders the eastern edge of this residential area."

if ((said( look) ||
	said( look, room) ||
	said( look, town)))	{
	print( 1);
	}


